<?xml version="1.0" encoding="utf-8"?>
<s:Application xmlns:fx="http://ns.adobe.com/mxml/2009" 		   
			   xmlns:s="library://ns.adobe.com/flex/spark" 
			   xmlns:gpc="com.goldpricecafe.mobile.*" 	
			   xmlns:mx="library://ns.adobe.com/flex/mx"
			   preloader="SlicedCirclePreloader"
			   resizeForSoftKeyboard="true"
			   initialize="init()" 
			   resize="resizeHandler()" 
			   creationComplete="creationComplatedHandler()"  >
	
	<s:layout>
		<s:VerticalLayout paddingLeft="2" paddingRight="2" paddingTop="2" paddingBottom="2"/>
	</s:layout>

	<fx:Style source="com/goldpricecafe/mobile/assets/goldpricecafe_skins.css"/>
	<fx:Style source="com/goldpricecafe/mobile/assets/goldpricecafe_styles.css"/>

	
	<fx:Metadata>	
	</fx:Metadata>
	
	<s:states>
		<s:State name="normal" />
		<s:State name="settings" />
		<s:State name="fullscreen"/>
	</s:states>
	
	<fx:Declarations>
		
		<s:RadioButtonGroup id="unitsRedioGroup" change="weightUnits = unitsRedioGroup.selectedValue.toString()" />
		<s:RadioButtonGroup id="chartTypeRadioGroup" change="chartType = chartTypeRadioGroup.selectedValue.toString()" />
		<s:RadioButtonGroup id="rangeRadioGroup" change="dataRange = Number(rangeRadioGroup.selectedValue)" />
		
		<s:NumberFormatter  id="buttonsCurrFormat"						  
							  decimalSeparator="." 
							  errorText="0.00" 
							  fractionalDigits="2" 
							  useGrouping="true"
							  groupingSeparator="," />
		<s:NumberFormatter id="amountFormat"  
							  decimalSeparator="." 
							  errorText="0.00" 
							  fractionalDigits="2" 
							  useGrouping="true"
							  groupingSeparator=","/>
	
		
		<s:Animate id="fader"
				   duration="500"
				   repeatBehavior="reverse"
				   repeatCount="0">
			<s:SimpleMotionPath property="alpha" valueFrom="1.0" valueTo="0.0" />
		</s:Animate>

	</fx:Declarations>
	
	<fx:Script>

		<![CDATA[	
			import com.goldpricecafe.mobile.Constants;
			import com.goldpricecafe.mobile.skins.ButtonBarFirstButton;
			
			import mx.collections.ArrayList;
			import mx.events.FlexEvent;
			import mx.events.PropertyChangeEvent;
			
			
			protected static const DATA_PROVIDER_CLASS:Class = com.goldpricecafe.mobile.BlazeDataProvider;
			
			//////////////////////////////////////////////////////
			//                                     				//
			//					Properties						//
			//													//
			//////////////////////////////////////////////////////
			
			/** 
			 * Chart type. Use following constants from com.goldpricecafe.mobile.Constants:
			 * GOLD, SILVER, PLATINUM, PALLDIUM, GLD_SLV_RATIO, GLD_PLT_RATIO 
			 */
			[Bindable]
			public function set chartType( type:String ) : void {
				
				_chartType = type;
				chartPanel.chartType = type;
				chartTypeRadioGroup.selectedValue = type;
				
				if(_chartType == Constants.MY_STACK) {
					updateMyStack();
				}
				
				if(_chartData) {
					chartPanel.chart.dataProvider = _chartData[_chartType];
				}
				
			}	
			public function get chartType() : String {
				
				return _chartType;
			}
			
			/**
			 * Weight units. Use following constants from com.goldpricecafe.mobile.Constants:
			 * UNITS_GR, UNITS_OZ
			 */
			[Bindable]
			public function set weightUnits( units:String ) : void {
				
				_weightUnits = units;
				chartPanel.weightUnits = units;
				unitsRedioGroup.selectedValue = units;

				updateMyStack();
				
			}	
			public function get weightUnits() : String {
				
				return _weightUnits;
			}
			
			/**
			 * Weight units. Use following constants from com.goldpricecafe.mobile.Constants:
			 * RANGE_3DAYS, RANGE_1DAY, RANGE_HISTORY
			 */			
			[Bindable]
			public function set dataRange( range:uint ) : void {
				
				_dataRange = range;
				rangeRadioGroup.selectedValue = range;
				chartPanel.dataRange = range;				
				
			}
			public function get dataRange() : uint {
				
				return _dataRange;
			}
			
			/**
			 * Currency. Use one of the values from com.goldpricecafe.mobile.Constants.CURRENCIES
			 */			
			[Bindable]
			public function set currency( currency:String ) : void {
				
				_currency = currency;
				chartPanel.currency = currency;
				currencyBtn.label = currency;
				
				updateChartData();			
				
			}	
			public function get currency( ) : String {
				
				return _currency;
				
			}
			
			/** Amount of gold in "My Stack" expressed in current weight units.
			 *  Internaly it is stored in ounces.
			 */
			[Bindable]
			public function set myStackGold( val:Number ) : void {
				
				if (weightUnits == Constants.UNITS_GR ) {
					_myStackGldOz = val * Constants.GROZ_RATIO;
				} else {
					_myStackGldOz = val;
				}
				
				updateMyStack();
				
			}
			public function get myStackGold() : Number {
				
				if (weightUnits == Constants.UNITS_GR ) {
					return _myStackGldOz / Constants.GROZ_RATIO;
				} else {
					return _myStackGldOz;
				}
				
			}	
			
			/** Amount of silver in "My Stack" expressed in current weight units.
			 *  Internaly it is stored in ounces.
			 */
			[Bindable]
			public function set myStackSilver( val:Number ) : void {
				
				if (weightUnits == Constants.UNITS_GR ) {
					_myStackSlvOz = val * Constants.GROZ_RATIO;
				} else {
					_myStackSlvOz = val;
				}
				
				updateMyStack();
				
			}
			public function get myStackSilver() : Number {
				
				if (weightUnits == Constants.UNITS_GR ) {
					return _myStackSlvOz / Constants.GROZ_RATIO;
				} else {
					return _myStackSlvOz;
				}
				
			}
			
			/** Amount of platinum in "My Stack" expressed in current weight units.
			 *  Internaly it is stored in ounces.
			 */
			[Bindable]
			public function set myStackPlatinum( val:Number ) : void {
				
				if (weightUnits == Constants.UNITS_GR ) {
					_myStackPltOz = val * Constants.GROZ_RATIO;
				} else {
					_myStackPltOz = val;
				}
				
				updateMyStack();
				
			}
			public function get myStackPlatinum() : Number {
				
				if (weightUnits == Constants.UNITS_GR ) {
					return _myStackPltOz / Constants.GROZ_RATIO;
				} else {
					return _myStackPltOz;
				}
				
			}
			
			/** Amount of palladium in "My Stack" expressed in current weight units.
			 *  Internaly it is stored in ounces.
			 */
			[Bindable]
			public function set myStackPalladium( val:Number ) : void {
				
				if (weightUnits == Constants.UNITS_GR ) {
					_myStackPldOz = val * Constants.GROZ_RATIO;
				} else {
					_myStackPldOz = val;
				}
				
				updateMyStack();
				
			}
			public function get myStackPalladium() : Number {
				
				if (weightUnits == Constants.UNITS_GR ) {
					return _myStackPldOz / Constants.GROZ_RATIO;
				} else {
					return _myStackPldOz;
				}
				
			}			
			
			[Bindable]
			public function set busy( val:Boolean ) : void {
				
				throbber.visible = val;
			}
			public function get busy() : Boolean {
				
				return throbber.visible;
			}
			
			
			//////////////////////////////////////////////////////
			//                                     				//
			//					Protected fields				//
			//													//
			//////////////////////////////////////////////////////
			
			protected var _screenSize:uint = Constants.SCREEN_BIG;
			
			/* Current data */
			
			protected var _dataProvider:IDataProvider;
			
			/* Currencies */
			
			protected var _currency:String;
			
			/* Chart type */
			
			protected var _chartType:String;
			
			/* Weight units */			
			
			protected var _weightUnits:String;
			
			/* Data ranges */
			
			protected var _dataRange:uint;
			
			/* Prices */
			
			protected var _gldPrices:Array;
			protected var _gldSlvRatios:Array;
			protected var _gldPltRatios:Array;
			protected var _gldPldRatios:Array;
			
			protected var _chartData:Object;
			protected var _currentGldPriceOz:Number = 0;
			protected var _currentSlvPriceOz:Number = 0;
			protected var _currentPltPriceOz:Number = 0;
			protected var _currentPldPriceOz:Number = 0;
			
			/* My stack */
			
			protected var _myStackGldOz:Number = 0;
			protected var _myStackSlvOz:Number = 0;
			protected var _myStackPldOz:Number = 0;
			protected var _myStackPltOz:Number = 0;
			
			/* Next update */
			
			protected var _nextUpdate:Date = new Date();
			protected var _timer:Timer = new Timer(1000);
			
			
			//////////////////////////////////////////////////////
			//                                     				//
			//					Protected methods				//
			//													//
			//////////////////////////////////////////////////////	
			

			protected function updateHeader() : void {
				
				var now:Date = new Date();
				var sec:int = Math.round( (_nextUpdate.getTime() - now.getTime() )/1000 );
				var min:int = sec/60;
				sec = sec % 60;
				
				if( min == 0 && sec <= 0 ) {
					
					timeLabel.text = "waiting";
					fader.end();
					fader.play([timeLabel]);
					
				} else {
					
					fader.end();
					timeLabel.alpha = 1.0;
					timeLabel.text = ((min < 10) ? "0" + min.toString() : min.toString()) + ":" +
									 ((sec < 10) ? "0" + sec.toString() : sec.toString());
				}
				
			}
			
			protected function updateButtons() : void {
				
				goldBtn.label = "Gld: " + buttonsCurrFormat.format(_currentGldPriceOz);
				silverBtn.label = "Slv: " + buttonsCurrFormat.format(_currentSlvPriceOz);				
				platinumBtn.label = "Plt: " + buttonsCurrFormat.format(_currentPltPriceOz);
				palladiumBtn.label = "Pld: " + buttonsCurrFormat.format(_currentPldPriceOz);				
			}
						
				
			protected function scaleStyles() : void {
				
				var ratio:Number = unscaledWidth/1024;
				
				if( styleManager ) {
					
					var style:CSSStyleDeclaration;
					
					style = styleManager.getStyleDeclaration( ".fontBig" );
					style.setStyle( "fontSize",Math.round( style.getStyle("fontSize")*ratio) );
					style = styleManager.getStyleDeclaration( ".fontMedium" );
					style.setStyle( "fontSize",Math.round( style.getStyle("fontSize")*ratio) );
					style = styleManager.getStyleDeclaration( ".fontSmall" );
					style.setStyle( "fontSize",Math.round( style.getStyle("fontSize")*ratio) );
				}
				
			}
			
			protected function updateMyStack() : void {
				
				var fract:uint;
				var formater:NumberFormatter = amountFormat;
				var maxChars:uint;
				var sum:Number = 0;
				
				/* Gold */
				pmcGoldLabel.text = 'Gold ' + firstUp(weightUnits) + ':';
				goldOzCalc.numericValue = myStackGold; 
				accGold.text = buttonsCurrFormat.format(_currentGldPriceOz * _myStackGldOz) + ' ' + currency.toUpperCase();	
				sum += _currentGldPriceOz * _myStackGldOz;
				
				/* Silver */
				pmcSilverLabel.text = 'Silver ' + firstUp(weightUnits) + ':';
				silverOzCalc.numericValue = myStackSilver; 
				accSilver.text = buttonsCurrFormat.format(_currentSlvPriceOz * _myStackSlvOz) + ' ' + currency.toUpperCase();
				sum += _currentSlvPriceOz * _myStackSlvOz;
				
				/* Platinum */
				pmcPlatinumLabel.text = 'Platinum ' + firstUp(weightUnits) + ':';
				platinumOzCalc.numericValue = myStackPlatinum; 
				accPlatinum.text = buttonsCurrFormat.format(_currentPltPriceOz * _myStackPltOz) + ' ' + currency.toUpperCase();	
				sum += _currentPltPriceOz * _myStackPltOz;
				
				/* Palladium */
				pmcPalladiumLabel.text = 'Palladium ' + firstUp(weightUnits) + ':';
				palladiumOzCalc.numericValue = myStackPalladium; 
				accPalladium.text = buttonsCurrFormat.format(_currentPldPriceOz * _myStackPldOz) + ' ' + currency.toUpperCase();
				sum += _currentPldPriceOz * _myStackPldOz;
				
				/* Total */
				totalWorth.text = buttonsCurrFormat.format( sum );
				
				/* Update chart if visible */
				if(chartType == Constants.MY_STACK) {
					chartMyStack();
					chartPanel.chart.dataProvider = _chartData[Constants.MY_STACK];
				}
				
			}
								
			protected function updateChartData() : void {
				
				if( !_dataProvider ) return;
				
				_chartData = _dataProvider.getData(currency);
				
				/* Find last prices */
				if(_chartData) {
					
					var i:int;
					
					i = _chartData[Constants.GOLD].length-1;
					while( (i>=0) && isNaN(_currentGldPriceOz = _chartData[Constants.GOLD][i][Constants.TODAY]) ) {
						i--;
					}
					i = _chartData[Constants.SILVER].length-1;
					while( (i>=0) && isNaN(_currentSlvPriceOz = _chartData[Constants.SILVER][i][Constants.TODAY]) ) {
						i--;
					}	
					i = _chartData[Constants.PLATINUM].length-1;
					while( (i>=0) && isNaN(_currentPltPriceOz = _chartData[Constants.PLATINUM][i][Constants.TODAY]) ) {
						i--;
					}
					i = _chartData[Constants.PALLADIUM].length-1;
					while( (i>=0) && isNaN(_currentPldPriceOz = _chartData[Constants.PALLADIUM][i][Constants.TODAY]) ) {
						i--;
					}					
				}
				
				/* Set chart data provider */				
				chartPanel.chart.dataProvider = new ArrayList(_chartData[chartType]);
				
				/* Update button labels */
				updateButtons();
				
				/* Update "My Stack" labels */
				updateMyStack();				
				
			}
			
			protected function chartMyStack() : void {
				
				if(!_chartData) return;
				
				_chartData[Constants.MY_STACK] = [];
				var days:Array = [Constants.TODAY,Constants.ONE_DAY_AGO,Constants.TWO_DAYS_AGO];
				
				for( var i:uint = 0; i < _chartData[Constants.GOLD].length; i++ ) {
					
					_chartData[Constants.MY_STACK][i] = {};
					_chartData[Constants.MY_STACK][i][Constants.TIME] = _chartData[Constants.GOLD][i][Constants.TIME];
					
					for( var j:int = 0; j<days.length; j++ ) {
						
						var day:String = days[j];
						_chartData[Constants.MY_STACK][i][day] = _chartData[Constants.GOLD][i][day] * _myStackGldOz +
																 _chartData[Constants.SILVER][i][day] * _myStackSlvOz +
																 _chartData[Constants.PALLADIUM][i][day] * _myStackPldOz +
																 _chartData[Constants.PLATINUM][i][day] * _myStackPltOz;
						
					}
				}
				
			}
						
			
			protected function firstUp( val:String ) : String {
				
				if( val && val.length > 0 ) {
					
					return val.substr(0,1).toUpperCase() +  val.substring(1);
					
				} else {
					
					return val;
				}
			}			
			
			//////////////////////////////////////////////////////
			//                                     				//
			//					Event handlers					//
			//													//
			//////////////////////////////////////////////////////
			
			protected function init() : void {
				
				currentState = "normal";
				_nextUpdate = new Date();
							
				/*if(!CustomStyleManager.loadStyles()) {
				CustomStyleManager.saveStyles();
				}*/
				
			}			
			
			protected function creationComplatedHandler() : void {
				
				/* Scale fonts to screen size */
				scaleStyles();
				
				/* Fill currencies popup */
				for( var i:uint = 0; i<Constants.CURRENCIES.length; i++ ) {
					
					var label:Label = new Label();
					label.text = Constants.CURRENCIES[i];
					label.styleName = "myLabel fontMedium";
					label.addEventListener(MouseEvent.CLICK,function(e:Event) : void {
						currencyPopUp.displayPopUp = false;
						currency = e.target.text.toUpperCase();
					});
					
					currencyChooser.addElement(label);
				}				
				
				chartType = Constants.GOLD;
				currency = "USD";
				weightUnits = Constants.UNITS_OZ;
				dataRange = Constants.RANGE_3DAYS;
				
				/* Initialize gestures support */
				Multitouch.inputMode = MultitouchInputMode.GESTURE;
				
				/* Initialize data provider */
				_dataProvider = new DATA_PROVIDER_CLASS();
				_dataProvider.addEventListener( "newData", newDataHandler );
				
				/* Iniialize MyStack */
				myStackGold = 0;
				myStackSilver = 0;
				
				/* Initialize timer */
				_timer.addEventListener( TimerEvent.TIMER,timerHandler );
				_timer.start();
				
				updateHeader();
				
			}
			
			protected function resizeHandler() : void {
				
			}	
			
			protected function newDataHandler( e:Event ) : void {
				
				_nextUpdate = _dataProvider.getNextUpdateTime();
				updateChartData();			
				updateHeader();
				
				busy = false;
				
			}
			
			protected function timerHandler( e:Event ) : void {
				
				updateHeader();
				
			}
				
			
			public override function styleChanged(style:String) : void {
				
				super.styleChanged(style); invalidateProperties();
				
			}
			
			
		]]>
	</fx:Script>
	
	<!--
	
	//////////////////////////////////////////////////////
	//                                     				//
	//						Skin						//
	//													//
	//////////////////////////////////////////////////////
	
	-->
	
	<s:VGroup id="normalVGroup" width="100%" height="100%" gap="0">
		
		<s:VGroup id="northPanel" width="100%" excludeFrom="settings" gap="0">
		
			<s:HGroup width="100%">
				<s:Label id="headerLabel" text="Next Live Price Update " styleName="myLabel fontBig" />
				<s:Spacer width="100%"/>
				<s:Label id="timeLabel" styleName="myLabel fontBig" />
			</s:HGroup>
			
			
			<s:HGroup id="metalBtnGroup" width="100%" verticalAlign="middle" excludeFrom="fullscreen,settings">
				
				<s:VGroup width="100%">	
					
					<s:HGroup width="100%">
						<!-- Curency button -->
						<s:Button id="currencyBtn" label="USD" width="40%" styleName="button bigButton fontMedium" click="popupFill.color = currencyBtn.getStyle('chromeColor'), currencyPopUp.displayPopUp = true" />	
						<!-- Metal buttons-->
						<s:RadioButton id="goldBtn" group="{chartTypeRadioGroup}" value="{Constants.GOLD}" width="100%" styleName="button bigButton fontMedium" label="Gld:" />
						<s:RadioButton id="silverBtn" group="{chartTypeRadioGroup}" value="{Constants.SILVER}" width="100%" styleName="button bigButton fontMedium" label="Slv:" />
						<s:RadioButton id="platinumBtn" group="{chartTypeRadioGroup}" value="{Constants.PLATINUM}" width="100%" styleName="button bigButton fontMedium" label="Plt:" />
						<s:RadioButton id="palladiumBtn" group="{chartTypeRadioGroup}" value="{Constants.PALLADIUM}" width="100%" styleName="button bigButton fontMedium" label="Pld:" />
					</s:HGroup>
					
					<!-- Currency popup -->
					<s:PopUpAnchor id="currencyPopUp" popUpPosition="below">
						<s:TileGroup width="{width/2}" height="{height/2}" id="currencyChooser" includeInLayout="false" clipAndEnableScrolling="true">
							<s:Rect x="0" y="0" width="{width/2}" height="{height/2}" includeInLayout="false">
								<s:fill>
									<s:SolidColor id="popupFill" color="{getStyle('backgroundColor')}" alpha="0.8"/>
								</s:fill>
							</s:Rect>
						</s:TileGroup>
					</s:PopUpAnchor>
					
				</s:VGroup>				
				
				<s:Spacer width="2"/>
				
				<!-- Units radio group -->
				<s:VGroup width="0%" styleName="fontSmall" gap="5" height="100%">			
					<s:RadioButton id="radioOunce" group="{unitsRedioGroup}" styleName="radioBtn fontVerySmall" label="OZ" value="{Constants.UNITS_OZ}" toolTip="Ounces" />
					<s:RadioButton id="radioGram" group="{unitsRedioGroup}" styleName="radioBtn fontVerySmall" label="GR" value="{Constants.UNITS_GR}" toolTip="Grams" />
				</s:VGroup>
				
			</s:HGroup>	
		
		</s:VGroup>

		<!-- Chart -->
		
		<gpc:Chart id="chartPanel" />
		
		<!-- Chart options group -->
		
		<s:VGroup id="southPanel" width="100%" excludeFrom="settings, fullscreen" gap="0">
			
			<s:HGroup id="chartDataBtnGroup" width="100%" verticalAlign="middle" gap="0">
				
				<!-- Chart data range buttons -->
				<!--s:ButtonBar id="dataRangeBar" requireSelection="true" change="dataRange = dataRangeBar.selectedIndex" styleName="button fontSmall">
					<s:ArrayCollection source="['Last 3 Days','Today','History']"/>
				</s:ButtonBar-->
				<s:RadioButton label="Last 3 Days" group="{rangeRadioGroup}" value="{Constants.RANGE_3DAYS}" skinClass="com.goldpricecafe.mobile.skins.ButtonBarFirstButton" styleName="button fontSmall"/>
				<s:RadioButton label="Today" group="{rangeRadioGroup}" value="{Constants.RANGE_1DAY}" skinClass="com.goldpricecafe.mobile.skins.ButtonBarMiddleButton" styleName="button fontSmall"/>
				<s:RadioButton label="History" group="{rangeRadioGroup}" value="{Constants.RANGE_HISTORY}" skinClass="com.goldpricecafe.mobile.skins.ButtonBarLastButton" styleName="button fontSmall"/>
				
				<s:Spacer width="7"/>
				
				<!-- Prices ratio buttons -->
				<s:RadioButton id="gldslvratioBtn" group="{chartTypeRadioGroup}" value="{Constants.GLD_SLV_RATIO}" label="Gld/Slv Ratio" width="100%" styleName="button fontSmall" />				
				
				<s:Spacer width="7"/>
				
				<s:RadioButton id="gldpltratioBtn" group="{chartTypeRadioGroup}" value="{Constants.GLD_PLT_RATIO}" label="Gld/Plt Ratio" width="100%" styleName="button fontSmall" />
				
				<s:Spacer width="7"/>
				
				<s:Button id="settingsBtn" label="Settings" width="100%" styleName="button fontSmall" click="currentState = 'settings'" />
				
			</s:HGroup>
			
			<!-- Track your stack -->
		
			<s:Spacer height="10"/>
			
			<s:Label id="trackYourStackLabel" text="Track Your Stack" styleName="myLabel fontSmall" />
			
			<s:HGroup id="trackYourStackGroup" width="100%" verticalAlign="justify" >
				
				<s:VGroup width="20%" height="100%" gap="2">
					<s:Label id="pmcGoldLabel" styleName="myLabel fontVerySmall" />
					<gpc:NumberInput id="goldOzCalc" maxChars="6" styleName="fontSmall" width="100%" numberFormater="{amountFormat}" numericValue="@{myStackGold}"/>
					<s:Label id="accGold" styleName="myLabel fontVerySmall" />				
				</s:VGroup>
				<s:VGroup width="20%" height="100%" gap="2">
					<s:Label id="pmcSilverLabel" text="{'Silver ' + firstUp(weightUnits) + ':'}" styleName="myLabel fontVerySmall" />
					<gpc:NumberInput id="silverOzCalc" maxChars="6" styleName="fontSmall" width="100%" numberFormater="{amountFormat}" numericValue="@{myStackSilver}"/>
					<s:Label id="accSilver" text="0.00 USD" styleName="myLabel fontVerySmall" />				
				</s:VGroup>
				<s:VGroup width="20%" height="100%" gap="2">
					<s:Label id="pmcPlatinumLabel" text="{'Platinum ' + firstUp(weightUnits) + ':'}" styleName="myLabel fontVerySmall" />
					<gpc:NumberInput id="platinumOzCalc" maxChars="6" styleName="fontSmall" width="100%" numberFormater="{amountFormat}" numericValue="@{myStackPlatinum}"/>
					<s:Label id="accPlatinum" text="0.00 USD" styleName="myLabel fontVerySmall" />				
				</s:VGroup>
				<s:VGroup width="20%" height="100%" gap="2">
					<s:Label id="pmcPalladiumLabel" text="{'Palladium ' + firstUp(weightUnits) + ':'}" styleName="myLabel fontVerySmall" />
					<gpc:NumberInput id="palladiumOzCalc" maxChars="6" styleName="fontSmall" width="100%" numberFormater="{amountFormat}" numericValue="@{myStackPalladium}"/>
					<s:Label id="accPalladium" text="0.00 USD" styleName="myLabel fontVerySmall" />				
				</s:VGroup>
				<s:VGroup width="20%" height="100%" gap="2">
					<s:Spacer height="100%"/>
					<s:RadioButton id="indivChartBtn" group="{chartTypeRadioGroup}" value="{Constants.MY_STACK}" label="Chart My Stack!" styleName="button bigButton fontMedium"/>
					<s:Label id="totalWorth" text="Total: 0.00 USD" styleName="myLabel fontVerySmall" />				
				</s:VGroup>		
				
			</s:HGroup>
			
		</s:VGroup>
		
		<!-- Settings screen -->
		
		<s:HGroup id="settingsHGroup" width="100%" includeIn="settings" >
			<gpc:SettingsPanel id="settingsPanel"/>
			<s:Spacer width="5" />
			<s:VGroup>
				<s:Button id="cancelBtn" label="Reset" styleName="button fontMedium" width="100%" click="settingsPanel.reset()" />
				<s:Button id="okBtn" label="OK" styleName="button fontMedium" width="100%" click="CustomStyleManager.saveStyles(); currentState = 'normal'" />
				<s:Spacer height="100%"/>
			</s:VGroup>
		</s:HGroup>	
		
	</s:VGroup>
	

	<!-- Busy screen -->
	
	<s:HGroup x="0" y="0" width="{width}" height="{height}" includeInLayout="false" excludeFrom="settings">
		<s:Spacer width="25%"/>
		<s:VGroup width="50%" height="100%">
			<s:Spacer height="20%"/>
			<s:BusyIndicator id="throbber" alpha="0.5" visible="true" width="100%" height="60%"/>
			<s:Spacer height="20%"/>			
		</s:VGroup>
		<s:Spacer width="25%"/>
	</s:HGroup>	
			
	
</s:Application>
