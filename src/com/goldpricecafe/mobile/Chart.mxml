<?xml version="1.0" encoding="utf-8"?>
<s:Group xmlns:fx="http://ns.adobe.com/mxml/2009" 
		 xmlns:s="library://ns.adobe.com/flex/spark" width="100%" height="100%"
		 xmlns:mx="library://ns.adobe.com/flex/mx"
		 initialize="init()"
		 creationComplete="creationCompleteHandler(event)"		 
		 gestureZoom="zoomGestureHandler(event)"
		 gesturePan="panGestureHandler(event)" >
	
	<s:layout>
		<s:VerticalLayout gap="0"/>
	</s:layout>
	
	<fx:Metadata>
		[Style(name="axisColor",type="Number",format="Color",inherit="yes")]
		[Style(name="gridColor",type="Number",format="Color",inherit="yes")]		
		[Style(name="serieTodayColor",type="Number",format="Color",inherit="yes")]
		[Style(name="serie1DayAgoColor",type="Number",format="Color",inherit="yes")]
		[Style(name="serie2DaysAgoColor",type="Number",format="Color",inherit="yes")]
		[Style(name="serieTodayWidth",type="Number",inherit="yes")]
		[Style(name="serie1DayAgoWidth",type="Number",inherit="yes")]
		[Style(name="serie2DaysAgoWidth",type="Number",inherit="yes")]		
	</fx:Metadata>
	
	<!--
	
	//////////////////////////////////////////////////////
	//                                     				//
	//					Declarations					//
	//													//
	//////////////////////////////////////////////////////
	
	-->
	
	<fx:Declarations>
		
		<s:SolidColorStroke id="serieTodayStroke" weight="{getStyle('serieTodayWidth')}" color="{getStyle('serieTodayColor')}" />		
		<s:SolidColorStroke id="serie1DayAgoStroke" weight="{getStyle('serie1DayAgoWidth')}" color="{getStyle('serie1DayAgoColor')}" />
		<s:SolidColorStroke id="serie2DaysAgoStroke" weight="{getStyle('serie2DaysAgoWidth')}" color="{getStyle('serie2DaysAgoColor')}" />
		<mx:CurrencyFormatter id="vertAxisCurrFormat" 
							  currencySymbol="" 
							  decimalSeparatorTo="." 
							  error="0.00" 
							  precision="2" 
							  thousandsSeparatorTo="," 
							  useThousandsSeparator="true" 
							  useNegativeSign="false" />		
		
	</fx:Declarations>
	
	<fx:Script>
		<![CDATA[
			
			import mx.charts.chartClasses.IAxis;
			import mx.charts.chartClasses.Series;
			import mx.events.FlexEvent;
			
			public static const STYLE_AXIS_COLOR:String ="axisColor";
			public static const STYLE_GRID_COLOR:String ="gridColor";
			public static const STYLE_SERIE_TODAY_COLOR:String = "serieTodayColor";
			public static const STYLE_SERIE_1_DAY_AGO_COLOR:String = "serie1DayAgoColor";
			public static const STYLE_SERIE_2_DAYS_AGO_COLOR:String = "serie2DaysAgoColor";
			public static const STYLE_SERIE_TODAY_WIDTH:String = "serieTodayWidth";
			public static const STYLE_SERIE_1_DAY_AGO_WIDTH:String = "serie1DayAgoWidth";
			public static const STYLE_SERIE_2_DAYS_AGO_WIDTH:String = "serie2DaysAgoWidth";
			
			public static var _zoomSpeed:Number = 0.25;
			
			//////////////////////////////////////////////////////
			//                                     				//
			//					Properties						//
			//													//
			//////////////////////////////////////////////////////
			
			/** 
			 * Chart type. Use following constants from com.goldpricecafe.mobile.Constants:
			 * GOLD, SILVER, PLATINUM, PALLDIUM, GLD_SLV_RATIO, GLD_PLT_RATIO 
			 */
			[Bindable]
			public function set chartType( type:String ) : void {
				
				_chartType = type;				
				updateChartTitle();
				forceRedraw();
				
			}	
			public function get chartType() : String {
				
				return _chartType;
			}			
			
			/**
			 * Weight units. Use following constants from com.goldpricecafe.mobile.Constants:
			 * UNITS_GR, UNITS_OZ
			 */
			[Bindable]
			public function set weightUnits( units:String ) : void {
				
				_weightUnits = units;			
				updateChartTitle();
				forceRedraw();
	
				
			}	
			public function get weightUnits() : String {
				
				return _weightUnits;
			}			
			
			/**
			 * Weight units. Use following constants from com.goldpricecafe.mobile.Constants:
			 * RANGE_3DAYS, RANGE_1DAY, RANGE_HISTORY
			 */			
			[Bindable]
			public function set dataRange( range:uint ) : void {
				
				_dataRange = range;
				updateChartTitle();
				
				switch( range ) {
					case Constants.RANGE_1DAY:
						serie1DayAgo.visible = serie2DaysAgo.visible = false;
						break;
					case Constants.RANGE_3DAYS:
						serie1DayAgo.visible = serie2DaysAgo.visible = true;
						break;	
					case Constants.RANGE_HISTORY:
						serie1DayAgo.visible = serie2DaysAgo.visible = false;
						break;					
				}				
				
			}
			public function get dataRange() : uint {
				
				return _dataRange;
			}
			
			/**
			 * Currency. Use one of the values from com.goldpricecafe.mobile.Constants.CURRENCIES
			 */			
			[Bindable]
			public function set currency( currency:String ) : void {
				
				_currency = currency;			
				updateChartTitle();

				
			}	
			public function get currency( ) : String {
				
				return _currency;
				
			}
			
			public function forceRedraw() : void {
				
				switch(_chartType) {
					
					case Constants.GLD_PLD_RATIO:
					case Constants.GLD_PLT_RATIO:
					case Constants.GLD_SLV_RATIO:
					case Constants.MY_STACK:
						
						_scale = 1;
						break;
					
					default:
						
						_scale = _weightUnits == Constants.UNITS_GR ? Constants.GROZ_RATIO : 1;
				}
				
				chart.series = chart.series;
				
			}
			
			//////////////////////////////////////////////////
			//                                     			//
			//					Protected 					//
			//												//
			//////////////////////////////////////////////////				
					
			protected var _dataRange:uint;
			protected var _weightUnits:String;
			protected var _currency:String;
			protected var _chartType:String;
			protected var _scale:Number = 1;
			protected var _zoom:Number = 1;
			protected var _pan:Number = 0;
			
			protected function currentDataPoint() : Point 
			{			
				var dataPoint:Array = serieToday.localToData( new Point(serieToday.mouseX,serieToday.mouseY) );		
				return new Point(dataPoint[0], dataPoint[1]);
				
			}
			
			protected function get baseWindowMin() : Number 
			{
				return 0;
			}
			
			protected function get baseWindowMax() : Number 
			{
				return 24*60;
			}
			
			protected function get minWindowWidth() : Number 
			{
				return 2;
			}
			
			protected function updateChartTitle() : void {
				
				var chartTitle:String;
				var axisTitle:String;
				
				if(  _chartType != null && _chartType.length > 0 ) {
					
					chartTitle = (_dataRange != Constants.RANGE_HISTORY) ? "Live " : "Historical ";
					
					switch(_chartType) {
						
						case Constants.GLD_PLT_RATIO:
							
							chartTitle += ("Gld/Plt Price Ratio");
							break;
						
						case Constants.GLD_SLV_RATIO:
							
							chartTitle += ("Gld/Slv Price Ratio");
							break;
						
						default:
							
							chartTitle += (_chartType + " Price");	
							axisTitle = "Price "
							
							if( _currency ) {						
								chartTitle += " in " + _currency.toUpperCase();
								axisTitle += " in " + _currency.toUpperCase();
							}
							if( _weightUnits ) {						
								chartTitle += "/" + weightUnits.toUpperCase();
								axisTitle += "/" + weightUnits.toUpperCase();
							}
							
					}
					
				} else {
					chartTitle = "";
				}
				
				//chartLabel.text = chartTitle;
				//verticalAxis.title = axisTitle;
				
			}
			
			public function zoom( scale:Number, center:Point ) : void {
				
				var startX:Number = Math.round(center.x -  (center.x - horizontalAxis.minimum) / scale );
				var endX:Number = Math.round(center.x + (horizontalAxis.maximum - center.x) / scale );
				
				// Zoom speed
				scale = Math.pow(scale,_zoomSpeed);
				
				if(startX < baseWindowMin) startX = baseWindowMin;
				if(endX > baseWindowMax) endX = baseWindowMax;
				
				horizontalAxis.minimum = startX;
				horizontalAxis.maximum = endX;
				
			}
			
			public function pan( offset:Number ) : void {
				
				var delta:Number = ( horizontalAxis.maximum - horizontalAxis.minimum ) * (offset / chart.width );
				var max:Number = horizontalAxis.maximum - delta;
				var min:Number = horizontalAxis.minimum - delta;	
				
				if( min < 0 ) {
					
					max = Math.min(24*60,max-min);
					min = 0;
					
				} else if( max > 24*60 ) {
					
					min = Math.max(0,min - (max - 24*60));
					max = 24*60;
				}
				
				horizontalAxis.minimum = min;
				horizontalAxis.maximum = max;							
				
			}
			
			public function seriesDataFunction( series:Series, item:Object, fieldName:String ):Object {
				
				var s:LineSeries = LineSeries( series );
				
				if( fieldName == "yValue" ) {
					return item[s.yField] * _scale;
				} else {
					return item[s.xField];
				}
				
			}
			
			public function hLabelFunction( val:Number, prevVal:Number, axis:IAxis ) : String {
				
				var hour:int = Math.round(val / 60);
				var min:int = val % 60;
				
				return ((hour < 10) ? "0" + hour.toString() : hour.toString()) + ":" + ((min < 10) ? "0" + min.toString() : min.toString());
				
			}
			
			public function vLabelFunction( val:Number, prevVal:Number, axis:IAxis ) : String {
				
				if( vertAxisCurrFormat ) {
					return vertAxisCurrFormat.format(val);
				} else {
					return "0.00";
				}
				
			}			
			
			public override function styleChanged( styleProp:String ) : void {
				
				super.styleChanged( styleProp );
				
				switch( styleProp ) {
					
					case STYLE_SERIE_TODAY_COLOR:
						serieTodayStroke.color = Number(getStyle( styleProp ));
						break;
					
					case STYLE_SERIE_1_DAY_AGO_COLOR:
						serie1DayAgoStroke.color = Number(getStyle( styleProp ));
						break;
					
					case STYLE_SERIE_2_DAYS_AGO_COLOR:
						serie2DaysAgoStroke.color = Number(getStyle( styleProp ));
						break;	
					
					case STYLE_SERIE_TODAY_WIDTH:
						serieTodayStroke.weight = Number(getStyle( styleProp ));
						break;
					
					case STYLE_SERIE_1_DAY_AGO_WIDTH:
						serie1DayAgoStroke.weight = Number(getStyle( styleProp ));
						break;
					
					case STYLE_SERIE_2_DAYS_AGO_WIDTH:
						serie2DaysAgoStroke.weight = Number(getStyle( styleProp ));
						break;
					
					case STYLE_AXIS_COLOR:
						horizontalAxisStroke.color = verticalAxisStroke.color = Number(getStyle( styleProp ));
						break;
					
					case STYLE_GRID_COLOR:
						horizontalGridStroke.color = verticalGridStroke.color = Number(getStyle( styleProp ));
						break;					
					
				}
	
			}
			
			//////////////////////////////////////////////////////
			//                                     				//
			//					Event handlers					//
			//													//
			//////////////////////////////////////////////////////			
			
			
			protected function init() : void  {
				
			
			}
			
			protected function creationCompleteHandler(event:FlexEvent):void {
				
			}			
			
			protected function mouseWheelHandler( e:MouseEvent ) : void {
				
				var p:Point = serieToday.globalToLocal( e.target.localToGlobal( new Point(e.localX, e.localY) ) );
				var a:Array = serieToday.localToData( p );
				var center:Point = new Point(a[0],a[1]);	
				var scale:Number = e["delta"] > 0 ? 1 + e["delta"]/24 : 1/(1 - e["delta"]/24);
				
				zoom(scale,center);
				
			}
			
			private var prevZoom:Number = 1;
			
			protected function zoomGestureHandler( e:TransformGestureEvent ) : void {
					
				switch(e.phase) {
					
					case "begin":
						break;
					case "end":
						break;
					default:
						
						var p:Point = serieToday.globalToLocal( e.target.localToGlobal( new Point(e.localX, e.localY) ) );
						var a:Array = serieToday.localToData( p );
						var center:Point = new Point(a[0],a[1]);	
					
						zoom(e.scaleX,center);	
				}				
				
			}
			
			protected function panGestureHandler( e:TransformGestureEvent ) : void {
		
				switch(e.phase) {
					
					case "begin":
						break;
					case "end":
						break;
					default:	
						pan(e.offsetX);
						
				}	
				

			}						
			
		]]>
	</fx:Script>
	
	
	<!--
	
	//////////////////////////////////////////////////////
	//                                     				//
	//					Skin							//
	//													//
	//////////////////////////////////////////////////////
	
	-->
	

	
	<!--s:Label id="chartLabel" width="100%" textAlign="center" styleName="myLabel fontMedium" /-->
	
	<mx:LineChart id="chart" width="100%" height="100%" cacheAsBitmap="true" showDataTips="true" mouseWheel="mouseWheelHandler(event)" gutterTop="5" gutterRight="30" >
		
		<!-- Vertical Axis -->
		<mx:verticalAxis>
			<mx:LinearAxis id="verticalAxis" baseAtZero="false" labelFunction="{vLabelFunction}" />
		</mx:verticalAxis>
		<mx:verticalAxisRenderers>					
			<mx:AxisRenderer id="verticalAxisRenderer" axis="{verticalAxis}" styleName="fontSmall">
				<mx:axisStroke>
					<mx:SolidColorStroke id="verticalAxisStroke" weight="1" color="{getStyle('color')}"/>
				</mx:axisStroke>							
			</mx:AxisRenderer>
		</mx:verticalAxisRenderers>	
		
		<!-- Horizontal Axis -->
		<mx:horizontalAxis>
			<mx:LinearAxis id="horizontalAxis" baseAtZero="false" maximum="{24*60}" labelFunction="{hLabelFunction}" autoAdjust="false"/>
		</mx:horizontalAxis>
		<mx:horizontalAxisRenderers>					
			<mx:AxisRenderer id="horizontalAxisRenderer" axis="{horizontalAxis}" styleName="fontSmall" >
				<mx:axisStroke>
					<mx:SolidColorStroke id="horizontalAxisStroke" weight="1" color="{getStyle('color')}"/>
				</mx:axisStroke>							
			</mx:AxisRenderer>
		</mx:horizontalAxisRenderers>
		
		<!-- Grid -->
		<mx:backgroundElements>
			<mx:GridLines id="gridlines" gridDirection="both">
				<mx:horizontalStroke>
					<mx:SolidColorStroke id="horizontalGridStroke" color="{getStyle('gridColor')}" weight="1" />						
				</mx:horizontalStroke>
				<mx:verticalStroke>
					<mx:SolidColorStroke id="verticalGridStroke" color="{getStyle('gridColor')}" weight="1"/>
				</mx:verticalStroke>
			</mx:GridLines>			
		</mx:backgroundElements>
		
		<!-- Series -->
		<mx:series>
			<mx:LineSeries id="serie2DaysAgo" interpolateValues="true" lineStroke="{serie2DaysAgoStroke}" xField="{Constants.TIME}" yField="{Constants.TWO_DAYS_AGO}" selectable="true" dataFunction="{seriesDataFunction}"/>
			<mx:LineSeries id="serie1DayAgo" interpolateValues="true" lineStroke="{serie1DayAgoStroke}" xField="{Constants.TIME}" yField="{Constants.ONE_DAY_AGO}" selectable="true" dataFunction="{seriesDataFunction}" />
			<mx:LineSeries id="serieToday" interpolateValues="true" lineStroke="{serieTodayStroke}" xField="{Constants.TIME}" yField="{Constants.TODAY}" selectable="true" dataFunction="{seriesDataFunction}" />	
		</mx:series>
		
	</mx:LineChart>	
</s:Group>
